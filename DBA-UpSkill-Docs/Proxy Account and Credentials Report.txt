IF OBJECT_ID('sp_searchProxies','P') IS NOT NULL 
    DROP PROCEDURE sp_searchProxies 
GO 
CREATE PROCEDURE sp_searchProxies 
 @ProxyName                SYSNAME = '%' 
,@ProxySubsystem        SYSNAME = '%' 
,@CredentialName        SYSNAME = '%' 
,@CredentialIdentity    SYSNAME = '%' 
,@JobName                SYSNAME = '%' 
 
AS 
 
IF LTRIM(RTRIM(@ProxyName)) = '' 
    SELECT @ProxyName = '%' 
 
IF LTRIM(RTRIM(@ProxySubsystem)) = '' 
    SELECT @ProxySubsystem = '%' 
     
IF LTRIM(RTRIM(@CredentialName)) = '' 
    SELECT @CredentialName = '%' 
 
IF LTRIM(RTRIM(@CredentialIdentity)) = '' 
    SELECT @CredentialIdentity = '%' 
 
IF LTRIM(RTRIM(@JobName)) = '' 
    SELECT @JobName = '%' 
 
    SELECT  Q.ProxyID 
           ,Q.ProxyName 
           ,ProxySubSystem = SUBSTRING(Q.ProxySubSystem,1,LEN(Q.ProxySubSystem)-1)  
           ,Q.CredentialName 
           ,Q.CredentialIdentity 
           ,Q.JobName 
           ,Q.JobStepName 
           ,Q.JobStepSubSystem 
      FROM 
    ( 
        SELECT ProxyID                = P.Proxy_id   
              ,ProxyName            = P.Name 
              ,ProxySubSystem = ( 
                 SELECT  Sub.subsystem + ',' AS [text()] 
                   FROM msdb.dbo.sysproxysubsystem Psub 
                   JOIN msdb.dbo.syssubsystems Sub 
                     ON Psub.subsystem_id = Sub.subsystem_id 
                  WHERE p.proxy_id = Psub.proxy_id 
                    FOR XML PATH('') 
                                )  
              ,CredentialName        = Cr.Name 
              ,CredentialIdentity    = Cr.Credential_Identity 
              ,JobName                = Job.Name 
              ,JobStepName            = Job.step_name 
              ,JobStepSubSystem        = Job.subsystem 
          FROM msdb.dbo.sysproxies p 
          JOIN sys.credentials Cr 
            ON Cr.Credential_Id = P.Credential_Id 
          LEFT JOIN ( 
               SELECT Jobs.Name 
                   ,JobStep.step_name 
                   ,JobStep.subsystem 
                   ,JobStep.Proxy_id 
               FROM msdb.dbo.sysjobsteps JobStep 
               JOIN msdb.dbo.sysjobs     Jobs 
                 ON JobStep.Job_id = Jobs.Job_id 
              WHERE JobStep.Proxy_id is not null 
              ) Job 
             ON Job.Proxy_id = p.proxy_id 
    ) Q 
  WHERE Q.ProxyName             LIKE @ProxyName 
    AND Q.ProxySubSystem     LIKE @ProxySubsystem 
    AND Q.CredentialName     LIKE @CredentialName 
    AND Q.CredentialIdentity LIKE @CredentialIdentity 
    AND ISNULL(Q.JobName,'')  LIKE @JobName 
GO 